plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "me.shedaniel.unified-publishing" version "0.1.+"
    id "com.github.breadmoirai.github-release" version "2.5.2"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        maven { url = "https://maven.boxadactle.dev/" }
        maven { url = "https://maven.terraformersmc.com/releases/" }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}


githubRelease {
    owner = "Boxadactle"
    repo = "coordinates-display"
    tagName = "$project.version"
    targetCommitish = "latest"
    releaseName = "Coordinates Display $project.version"
    generateReleaseNotes = true
    body = """Coordinates Display $project.version for Minecraft $project.minecraft_version

${new File(rootProject.rootDir, project.changelog_file).text}
"""
    authorization = "Token ${System.getenv("GITHUB_TOKEN")}"

    var files = []
    for (String platform : project.enabled_platforms.split(',')) {
        files += fileTree("$platform/build/libs") {
            include "*$project.version*"
        }
    }
    releaseAssets = files

//    dryRun = true
}